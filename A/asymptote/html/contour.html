<html lang="en">
<head>
<title>contour - Asymptote: the Vector Graphics Language</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Asymptote: the Vector Graphics Language">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="Base-modules.html#Base-modules" title="Base modules">
<link rel="prev" href="flowchart.html#flowchart" title="flowchart">
<link rel="next" href="contour3.html#contour3" title="contour3">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<!--
This file documents `Asymptote', version 2.15.

`http://asymptote.sourceforge.net'

Copyright (C) 2004-11 Andy Hammerlindl, John Bowman, and Tom Prince.

     Permission is granted to copy, distribute and/or modify this
     document under the terms of the GNU Lesser General Public License.
     On Debian systems, the LGPL can be found at
     /usr/share/common-licenses/LGPL.

   -->
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="contour"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="contour3.html#contour3">contour3</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="flowchart.html#flowchart">flowchart</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Base-modules.html#Base-modules">Base modules</a>
<hr>
</div>

<h3 class="section">8.36 <code>contour</code></h3>

<p><a name="index-g_t_0040code_007bcontour_007d-1216"></a>This package draws contour lines. 
To construct contours corresponding to the values in a real array <code>c</code>
for a function <code>f</code> on <code>box(a,b)</code>, use the routine
<pre class="verbatim">guide[][] contour(real f(real, real), pair a, pair b,
                  real[] c, int nx=ngraph, int ny=nx,
                  interpolate join=operator --, int subsample=1);
</pre>
The integers <code>nx</code> and <code>ny</code> define the resolution. 
The default resolution, <code>ngraph x ngraph</code> (here <code>ngraph</code>
defaults to <code>100</code>) can be increased for greater accuracy. 
The default interpolation operator is <code>operator --</code> (linear). Spline
interpolation (<code>operator ..</code>) may produce smoother
contours but it can also lead to overshooting. 
The <code>subsample</code> parameter indicates the number of interior points
that should be used to sample contours within each <code>1 x 1</code> box;
the default value of <code>1</code> is usually sufficient.

   <p>To construct contours for an array of data values on a uniform
two-dimensional lattice on <code>box(a,b)</code>, use
<pre class="verbatim">guide[][] contour(real[][] f, pair a, pair b, real[] c,
                  interpolate join=operator --, int subsample=1);
</pre>

   <p>To construct contours for an array of data values on a nonoverlapping
regular mesh specified by the two-dimensional array <code>z</code>,
<pre class="verbatim">guide[][] contour(pair[][] z, real[][] f, real[] c,
                  interpolate join=operator --, int subsample=1);

</pre>
To construct contours for an array of values <code>f</code> specified at
irregularly positioned points <code>z</code>, use the routine
<pre class="verbatim">guide[][] contour(pair[] z, real[] f, real[] c,
                  interpolate join=operator --, int subsample=1);
</pre>
The contours themselves can be drawn with one of the routines
<pre class="verbatim">void draw(picture pic=currentpicture, Label[] L=new Label[],
          guide[][] g, pen p=currentpen);

void draw(picture pic=currentpicture, Label[] L=new Label[],
          guide[][] g, pen[] p);
</pre>

   <p>The following simple example draws the contour at value <code>1</code>
for the function z=x^2+y^2, which is a unit circle:

<pre class="verbatim">import contour;
size(75);

real f(real a, real b) {return a^2+b^2;}
draw(contour(f,(-1,-1),(1,1),new real[] {1}));
</pre>
   <pre class="sp">

</pre>
<div align="center"><img src="onecontour.png" alt="onecontour.png"></div>

<p>The next example draws and labels multiple contours for the function
z=x^2-y^2 with the resolution <code>100 x 100</code>, using a dashed
pen for negative contours and a solid pen for positive (and zero) contours:

<pre class="verbatim">import contour;

size(200);

real f(real x, real y) {return x^2-y^2;}
int n=10;
real[] c=new real[n];
for(int i=0; i &lt; n; ++i) c[i]=(i-n/2)/n;

pen[] p=sequence(new pen(int i) {
    return (c[i] >= 0 ? solid : dashed)+fontsize(6pt);
  },c.length);

Label[] Labels=sequence(new Label(int i) {
    return Label(c[i] != 0 ? (string) c[i] : "",Relative(unitrand()),(0,0),
                 UnFill(1bp));
  },c.length);

draw(Labels,contour(f,(-1,-1),(1,1),c),p);
</pre>
   <pre class="sp">

</pre>
<div align="center"><img src="multicontour.png" alt="multicontour.png"></div>

<p>The next example illustrates how contour lines can be drawn on color
density images:

<pre class="verbatim">import graph;
import palette;
import contour;

size(10cm,10cm,IgnoreAspect);

pair a=(0,0);
pair b=(2pi,2pi);

real f(real x, real y) {return cos(x)*sin(y);}

int N=200;
int Divs=10;
int divs=2;

defaultpen(1bp);
pen Tickpen=black;
pen tickpen=gray+0.5*linewidth(currentpen);
pen[] Palette=BWRainbow();

bounds range=image(f,Automatic,a,b,N,Palette);
    
// Major contours

real[] Cvals=uniform(range.min,range.max,Divs);
draw(contour(f,a,b,Cvals,N,operator --),Tickpen);

// Minor contours
real[] cvals;
for(int i=0; i &lt; Cvals.length-1; ++i)
  cvals.append(uniform(Cvals[i],Cvals[i+1],divs)[1:divs]);
draw(contour(f,a,b,cvals,N,operator --),tickpen);

xaxis("$x$",BottomTop,LeftTicks,above=true);
yaxis("$y$",LeftRight,RightTicks,above=true);

palette("$f(x,y)$",range,point(NW)+(0,0.5),point(NE)+(0,1),Top,Palette,
        PaletteTicks(N=Divs,n=divs,Tickpen,tickpen));
</pre>
   <pre class="sp">

</pre>
<div align="center"><img src="imagecontour.png" alt="imagecontour.png"></div>

<p>Finally, here is an example that illustrates the construction of contours
from irregularly spaced data:

<pre class="verbatim">import contour;

size(200);

int n=100;

real f(real a, real b) {return a^2+b^2;}

srand(1);

real r() {return 1.1*(rand()/randMax*2-1);}

pair[] points=new pair[n];
real[] values=new real[n];

for(int i=0; i &lt; n; ++i) {
  points[i]=(r(),r());
  values[i]=f(points[i].x,points[i].y);
}

draw(contour(points,values,new real[]{0.25,0.5,1},operator ..),blue);
</pre>
   <pre class="sp">

</pre>
<div align="center"><img src="irregularcontour.png" alt="irregularcontour.png"></div>

<p>In the above example, the contours of irregularly spaced data are constructed by
first creating a triangular mesh from an array <code>z</code> of pairs:

   <p><a name="index-g_t_0040code_007btriangulate_007d-1217"></a>
<pre class="verbatim">int[][] triangulate(pair[] z);
</pre>

<pre class="verbatim">size(200);
int np=100;
pair[] points;

real r() {return 1.2*(rand()/randMax*2-1);}

for(int i=0; i &lt; np; ++i)
  points.push((r(),r()));

int[][] trn=triangulate(points);

for(int i=0; i &lt; trn.length; ++i) {
  draw(points[trn[i][0]]--points[trn[i][1]]);
  draw(points[trn[i][1]]--points[trn[i][2]]);
  draw(points[trn[i][2]]--points[trn[i][0]]);
}

for(int i=0; i &lt; np; ++i)
  dot(points[i],red);
</pre>
   <pre class="sp">

</pre>
<div align="center"><img src="triangulate.png" alt="triangulate.png"></div>

<p>The example <code>Gouraudcontour</code> illustrates how to produce color
density images over such irregular triangular meshes. 
<code>Asymptote</code> uses a robust version of Paul Bourke's Delaunay triangulation
algorithm based on the public-domain exact arithmetic predicates written by
Jonathan Shewchuk.

   </body></html>

